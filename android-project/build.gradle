// Basic Gradle setup.
buildscript {
	repositories {
		mavenCentral()
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:7.0.4'
	}
}

allprojects {
	repositories {
		mavenCentral()
		google()
	}
}

buildDir = "../build/android/${project.name}"
subprojects {
	buildDir = "../../build/android/${project.name}"
}

// The HamSandwich Android app itself.
apply plugin: 'com.android.application'

dependencies {
	implementation project(':android-project:SDL2')
	implementation 'androidx.appcompat:appcompat:1.4.1'
	implementation 'com.google.android.material:material:1.5.0'
	implementation 'com.google.android.gms:play-services-basement:18.0.1'
}

android {
	compileSdkVersion 31
	ndkVersion "24.0.8215888"

	defaultConfig {
		// Modify this line to distinguish your app from the upstream.
		// Example: 'io.github.YourUsername.hamsandwich'
		applicationId 'com.platymuus.hamsandwich'
		minSdk 16
		targetSdkVersion 31
		externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
	}

	externalNativeBuild.cmake {
		version '3.21.2'
		path '../CMakeLists.txt'
		buildStagingDirectory "${buildDir}.staging"
	}

	sourceSets.main {
		assets.srcDir "../assets/android"
		// Our CMake build puts assets to embed in the .apk here at build time.
		assets.srcDir "${buildDir}/intermediates/cxx/hamsandwich-assets"
	}

	// Self-sign release APKs.
	if (System.getenv('ANDROID_KEYSTORE')) {
		signingConfigs {
			release {
				storeFile file(System.getenv('ANDROID_KEYSTORE'))
				keyAlias "hamsandwich"
				storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
				keyPassword System.getenv('ANDROID_KEYSTORE_PASSWORD')

				v1SigningEnabled true
				v2SigningEnabled true
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
			}
		}
	}
}

// Building this project depends on building SDL2 first.
// Merging assets depends on building first.
tasks.configureEach { task ->
	if (task.name == 'buildCMakeDebug') {
		task.dependsOn ':android-project:SDL2:externalNativeBuildDebug'
	} else if (task.name == 'buildCMakeRelWithDebInfo') {
		task.dependsOn ':android-project:SDL2:externalNativeBuildRelease'
	} else if (task.name == 'mergeDebugAssets') {
		task.dependsOn ':android-project:buildCMakeDebug'
	} else if (task.name == 'mergeReleaseAssets') {
		task.dependsOn ':android-project:buildCMakeRelWithDebInfo'
	}
}
